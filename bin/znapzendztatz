#!/usr/bin/env perl

use FindBin;
# PERL5LIB
use lib "$FindBin::Bin/../lib";
use lib "$FindBin::Bin/../thirdparty/lib/perl5";

use Getopt::Long 2.25 qw(:config posix_default no_ignore_case);
use Pod::Usage 1.14;

use Mojo::Base -strict;
use ZnapZend::Config;
use ZnapZend::ZFS;

my $zConfig = ZnapZend::Config->new();
my $zZfs    = ZnapZend::ZFS->new();
my $zTime   = ZnapZend::Time->new();

sub dumpStats {
    my $stats = shift;

    print ' ' x (5- length($stats->{count})) . $stats->{count};
    print ' ' x (14 - length($stats->{usage})) . $stats->{usage};
    print "   $stats->{dataset}\n";
}

sub collectData {
    my $dataset = shift;
    my $snapFilter = shift;
    my %data;
    my $snapshots = $zZfs->listSnapshots($dataset, $snapFilter);

    $data{count} = $#{$snapshots};
    ${$snapshots}[-1] =~ /^.*\@($snapFilter)$/;
    my $snapshotInterval = $snapshots->[0] . '%' . $1;
    $data{usage} = $zZfs->snapshotReclaim($snapshotInterval);
    $data{dataset} = $dataset;

    return \%data;
}

sub main {
    my $opts = {};

    GetOptions($opts, qw(help man)) or exit 1;

    $opts->{help} && do {
        pod2usage(-exitval => 'NOEXIT');

### RM_COMM_4_TEST ###  # remove ### RM_COMM_4_TEST ### comments for testing purpose.
### RM_COMM_4_TEST ###  $opts = {};

        return 1;
    };
    $opts->{man} && pod2usage(-exitstatus => 0, -verbose => 2);

    my $backupSets = $zConfig->getBackupSet(pop @ARGV)
        or die "ERROR: cannot list backup config\n";

    #check if there is at least one valid backup set
    @$backupSets or die "ERROR: no valid znapzend setup found on source\n";

    #print header
    print "snapshot\ncount   space usage   dataset\n";

    for my $backupSet (@$backupSets){
        my $datasets = $backupSet->{recursive} eq 'on'
            ? $zZfs->listSubDataSets($backupSet->{src}) : [$backupSet->{src}];

        my $snapFilter = $zTime->getSnapshotFilter($backupSet->{tsformat});

        #source dataset
        for my $dataset (@$datasets){
            dumpStats(collectData($dataset, $snapFilter));

            #destination datasets
            for (keys %$backupSet){
                my ($key) = /^(dst_[^_]+)$/ or next;

                #skipping destination if it does not exists
                if (!$backupSet->{$key . '_valid'}){
                    print "*** WARNING: destination '$backupSet->{$key}'"
                        . " does not exist! ***\n\n";
                }
                else{
                    my $dstDataSet = $dataset;
                    $dstDataSet =~ s/^$backupSet->{src}/$backupSet->{$key}/;
                    dumpStats(collectData($dstDataSet, $snapFilter));
                }
            }
              
        }
    }
    return 1;
}

main();

1;

__END__

=head1 NAME

znapzendztatz - znapzend statistics utility

=head1 SYNOPSIS

B<znapzendztatz> [src_dataset]

    --help
    --man

=head1 DESCRIPTION

znapzendztatz shows statistics of snapshots created and storage space usage

=head1 COPYRIGHT

Copyright (c) 2014 by OETIKER+PARTNER AG. All rights reserved.

=head1 LICENSE

This program is free software: you can redistribute it and/or modify it
under the terms of the GNU General Public License as published by the Free
Software Foundation, either version 3 of the License, or (at your option)
any later version.

This program is distributed in the hope that it will be useful, but WITHOUT
ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for
more details.

You should have received a copy of the GNU General Public License along with
this program. If not, see L<http://www.gnu.org/licenses/>.

=head1 AUTHOR

S<Tobias Oetiker E<lt>tobi@oetiker.chE<gt>>
S<Dominik Hassler>

=head1 HISTORY

2014-06-29 had Flexible snapshot time format
2014-06-05 had Initial Version

=cut

